plugins {
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "io.github.goooler.shadow" version "8.1.7" apply false
    id "com.palantir.git-version" version "0.12.3"
}

repositories {
    mavenCentral()
}

version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+$/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

def embulkVersion = "0.10.36"

dependencies {
    compileOnly "org.embulk:embulk-api:$embulkVersion"
    compileOnly "org.embulk:embulk-spi:$embulkVersion"

    implementation "org.embulk:embulk-util-config:0.3.1"
    implementation "org.embulk:embulk-util-json:0.1.1"
    implementation "org.embulk:embulk-util-timestamp:0.2.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7" // Need to specify explicitly
    implementation "com.google.code.gson:gson:2.9.0"
    implementation "com.google.guava:guava:31.1-jre"

    implementation project(path: ":shadow-kintone-java-client", configuration: "shadow")

    testImplementation "org.embulk:embulk-api:$embulkVersion"
    testImplementation "org.embulk:embulk-spi:$embulkVersion"
    testImplementation "org.embulk:embulk-junit4:$embulkVersion"
    testImplementation "org.embulk:embulk-core:$embulkVersion"
    testImplementation "org.embulk:embulk-deps:$embulkVersion"
    testImplementation "org.mockito:mockito-core:4.6.1"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = "6.14.1"
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

// Workaround for IntelliJ issue
// https://youtrack.jetbrains.com/issue/IDEA-163411
// https://github.com/johnrengelman/shadow/issues/264#issuecomment-814402906
compileJava.dependsOn(tasks.getByPath(":shadow-kintone-java-client:publishToMavenLocal"))

embulkPlugin {
    mainClass = "org.embulk.input.kintone.KintoneInputPlugin"
    category = "input"
    type = "kintone"
}

gem {
    authors = [ "giwa" ]
    email = [ "ugw.gi.world@gmail.com" ]
    summary = "Kintone input plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-input-kintone"
    licenses = [ "MIT" ]
}
